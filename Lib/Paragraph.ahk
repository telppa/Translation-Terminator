/*
; ver. 2021.10.31
; 以下是常见的一段文字被切割成多个单行的场景
; https://www.doc88.com/p-3062268332883.html
; pdf 中引用参考文献
test1=
(
esophageal varices? A randomized controlled study. Dig Dis
Sci 1997;42:536–41.
33. Horst D, Grace NE, Conn HO, et al. Comparison of dietary
protein with an oral, branched-chain enriched amino acid
supplement in chronic portal-systemic encephalopathy. Hepa-
tology 1984;2:279–87.
34. Van Thiel DH, Fagiuoli S, Wright HI, et al. Gastrointestinal
transit in cirrhotic patients: Effect of hepatic encephalopathy
and its treatment. Hepatology 1994;19:67–71.
35. Wolpert E, Phillips SF, Summerskill WH. Ammonia produc-
tion in the human colon. Effects of cleansing, neomycin and
acetohydroxamic acid. N Engl J Med 1970;283:159–64.
36. Rolachon A, Zarski JP, Lutz JM, et al. [Is the intestinal lavage
with a solution of mannitol effective in the prevention of
post-hemorrhagic hepatic encephalopathy in patients with
liver cirrhosis? Results of a randomized prospective study].
Gastroenterol Clin Biol 1994;18:1057–62.
37. Cordoba J, Blei AT. Treatment of hepatic encephalopathy.
Am J Gastroenterol 1997;92:1429–39.
38. Riordan SM, Williams R. Treatment of hepatic encephalopa-
thy. N Engl J Med 1997;337:473–9.
39. Blanc P, Daures JP, Liautard J, et al. [Lactulose-neomycin
combination versus placebo in the treatment of acute hepatic
encephalopathy. Results of a randomized controlled trial].
Gastroenterol Clin Biol 1994;18:1063–8.
40. Clausen MR, Mortensen PB. Lactulose, disaccharides and
colonic f l ora. Clinical consequences. Drugs 1997;53:930–42.
41. Warren SE, Mitas JA 2d, Swerdlin AH. Hypernatremia in
hepatic failure. JAMA 1980;243:1257–60.
42. Uribe M, Campollo A, Vargas F, et al. Acidifying enemas
(Lactitol and lactose) vs. non-acidifying enemas (tap water) to
treat acute portal-systemic encephalopathy: A double-blind
randomized clinical trial. Hepatology 1987;7:639–43.
43. Hawkins RA, Jessy J, Mans AM, et al. Neomycin reduces the
intestinal production of ammonia from glutamine. Adv Exp
Med Biol 1994;368:125–34.
44. Morgan MH, Read AE, Speller DC. Treatment of hepatic
encephalopathy with metronidazole. Gut 1982;23:1–7.
45. Gubbins GP, Moritz TE, Marsano LS, et al. Helicobacter
pylori is a risk factor for hepatic encephalopathy in acute
alcoholic hepatitis: The ammonia hypothesis revisited. The
Veterans Administration Cooperative Study Group No. 275.
Am J Gastroenterol 1993;88:1906–10.
46. Vasconez C, Elizalde JI, Llach J, et al. Helicobacter pylori,
hyperammonemia and subclinical portosystemic encephal-
opathy: Effects of eradication. J Hepatol 1999;30:260–4.
47. Loft S, Sonne J, Dossing M, Andreasen PB. Metronidazole
pharmacokinetics in patients with hepatic encephalopathy.
Scand J Gastroenterol 1987;22:117–23.
48. Kircheis G, Nilius R, Held C, et al. Therapeutic eff i cacy of
L-ornithine-L-aspartate infusions in patients with cirrhosis and
hepatic encephalopathy: Results of a placebo-controlled, dou-
ble-blind study. Hepatology 1997;25:1351–60.
49. Stauch S, Kircheis G, Adler G, et al. Oral L-ornithine-L-
aspartate therapy ofchronic hepaticencephalopathy. Results of
a placebo-controlled double-blind study. J Hepatol 1998;28:
856–64.
50. Schafer DF, Jones EA. Hepatic encephalopathy and the gam-
ma-amino-butyric acid system. Lancet 1982;1:18–20.
51. Basile AS, Hughes RD, Harrison PM, et al. Elevated brain
concentrations of 1,4-benzodiazepines in fulminant hepatic
failure. N Engl J Med 1991;325:473–8.
52. Barbaro G, Di Lorenzo G, Soldini M, et al. Flumazenil for
hepatic encephalopathy grade III and Iva in patients with
cirrhosis: An Italian multicenter double-blind, placebo-con-
trolled, cross-over study. Hepatology 1998;28:374–8.
53. James JH, Ziparo V, Jeppsson B, Fischer JE. Hyperammon-
)

; pdf 中正文
test2=
(
PREAMBLE
Guidelines for clinical practice are intended to suggest pref-
erable approaches to particular medical problems as estab-
lished by interpretation and collation of scientif i cally valid
research, derived from extensive review of the published
literature. When data are not available that will withstand
objective scrutiny, a recommendation may be made based
on a consensus of experts. Guidelines are intended to apply
to the clinical situation for all physicians without regard to
specialty. Guidelines are intended to be f l exible, not neces-
sarily indicating the only acceptable approach, and should
be distinguished from standards of care that are inf l exible
and rarely violated. Given the wide range of choices in any
health care problem, the physician should select the course
best suited to the individual patient and the clinical situation
presented. These guidelines are developed under the aus-
pices of the American College of Gastroenterology and its
Practice Parameters Committee. These guidelines are also
approved by the governing board of the American Gastro-
enterological Association. Expert opinion is solicited from
the outset for the document. Guidelines are reviewed in
depth by the committee, with participation from experienced
clinicians and others in related f i elds. The f i nal recommen-
dations are based on the data available at the time of the
production of the document and may be updated with per-
tinent scientif i c developments at a later time. The following
guidelines are intended for adults and not for pediatric
patients. (Am J Gastroenterol 2001;96:1968–1976. © 2001
by Am. Coll. of Gastroenterology)
INTRODUCTION
Hepatic encephalopathy (HE) may be def i ned as a distur-
bance in central nervous system function because of hepatic
insuff i ciency. This broad def i nition ref l ects the existence of
a spectrum of neuropsychiatric manifestations related to a
range of pathophysiological mechanisms. Present in both
acute and chronic liver failure, these neuropsychiatric man-
ifestations are potentially reversible.
Multiple treatments have been used for HE. However,
their eff i cacy has been infrequently assessed by well-de-
signed randomized clinical trials. This handicap ref l ects the
diff i culty in evaluating the wide range of neuropsychiatric
symptomatology. Alteration ofconsciousness—its most rel-
evant manifestation—undergoes spontaneous f l uctuations
and will be inf l uenced by concurrent clinical factors, such as
infection, hypoxemia, GI hemorrhage, or electrolyte distur-
bances. Despite these limitations, a critical appraisal of
available data renders it possible to delineate a rational
approach to the treatment of HE.
CLINICAL CONSIDERATIONS
Pathophysiology
The main tenet of all theories of the pathogenesis of HE is
f i rmly accepted: nitrogenous substances derived from the
gut adversely affect brain function. These compounds gain
access to the systemic circulation as a result of decreased
hepatic function or portal-systemic shunts. Once in brain
tissue, they produce alterations of neurotransmission that
affect consciousness and behavior. Abnormalities in gluta-
matergic, serotoninergic,  -aminobutyric acid–ergic
(GABA-ergic), and catecholamine pathways, among others,
have been described in experimental HE (1). The research
challenge lies in the dissection of each of these systems and
their possible pharmacological manipulation to improve
treatment.
A large body of work points at ammonia as a key factor
in the pathogenesis of HE (2, 3). Ammonia is released from
several tissues (kidney, muscle), but its highest levels can be
found in the portal vein. Portal ammonia is derived from
both the urease activity of colonic bacteria and the deami-
dation of glutamine in the small bowel, and is a key sub-
strate for the synthesis of urea and glutamine in the liver.
The hepatic process is eff i cient, with a f i rst pass extraction
of ammonia of approximately 0.8 (4). In acute and chronic
liver disease, increased arterial levels of ammonia are com-
monly seen. In fulminant hepatic failure (FHF), elevated
arterial levels (200  g/dl) have been associated with an
increased risk of cerebral herniation (5). However, correla-
tion of blood levels with mental state in cirrhosis is inac-
curate. The blood-brain barrier permeability to ammonia is
increased in patients with HE (6); as a result, blood levels
will correlate weakly with brain values, though recent stud-
ies indicate an improvement ofthis correlation by correcting
the ammonia value to the blood pH (7). Furthermore, the
alterations in neurotransmission induced by ammonia also
occur after the metabolism of this toxin into astrocytes (3),
)

; 代码中注释
test3=
(
  ; MIT License
  ;
  ; Permission is hereby granted, free of charge, to any person obtaining a copy
  ; of this software and associated documentation files (the "Software"), to deal
  ; in the Software without restriction, including without limitation the rights
  ; to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  ; copies of the Software, and to permit persons to whom the Software is
  ; furnished to do so, subject to the following conditions:
  ;
  ; The above copyright notice and this permission notice shall be included in all
  ; copies or substantial portions of the Software.
  ;
  ; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  ; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  ; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  ; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  ; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  ; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  ; SOFTWARE.
)

; ocr 的结果
test4=
(
很多人认为，没时间没精力去运动瘦身，那就吃
点健康食品吧，把碳酸饮料和烤肉都戒掉，总可
以瘦了吧？
不瞒你说，还真的不可以。
因为那些你认为的健康饮食，可能会让你更胖。
澳大利亚导演达蒙（Damon Gameau）做的一个
真人实验。
为了弄清楚糖对身体的影响，达蒙把自己当成白
老鼠，在医生、营养师和血液专家的监督下，每
天摄入40茶匙的糖（约160克），坚持60天，看
看身体会发生什么变化。
40茶匙的糖，听起来很多，其实相当于澳大利
亚人每天糖摄入量的平均水平。而且医生规定，
这些糖的来源，不能是高糖垃圾食品，比如雪糕
汽水等，必须选择公认为健康的食品和饮料。
)

for k, v in Paragraph.merge(test1)
  MsgBox, % v

ExitApp
*/
Class Paragraph
{
  /*
  1. 对 a 去注释得到 a2
  2. 对 a2 按空行分段得到 a3 b3 c3
  3. 对 a3 按序号进行分离得到 a4
  4. 对 a4 按宽度进行合并得到 a5
  5. 对 b3 c3 重复3-4
  */
  merge(Text, Font:="", FontSize:="", ShortLineRatio:=0.9, LongLineRatio:=1.5)
  {
    ret:=[]
    
    ; 统一换行符 `r`n 为 `n
    Text1:=StrReplace(Text, "`r`n", "`n")
    
    ; 统一换行符 `r 为 `n
    Text1:=StrReplace(Text1, "`r", "`n")
    
    ; 删除行首注释符
    Text1:=this._deleteEscape(Text1)
    
    /* 像下面这种存在用空行分段的情况，则预先分段
    aaaaaaa
    bbbbb
    
    ccccccc
    */
    oParagraph1:=StrSplit(Text1, "`n`n", " `t`r`n`v`f")
    
    ; 这里如果使用 for k, paragraph 则会在某些时候把 Class 自身给覆盖掉
    for k, paragraph1 in oParagraph1
    {
      oLines:=StrSplit(paragraph1, "`n")
      
      ; 以递增的序号为标记，再次分段
      oParagraph2:=this._splitBySerialNumber(oLines)
      
      ; 根据行的长度进行合并
      for k, paragraph2 in oParagraph2
        ret.Push(this._mergeByLen(paragraph2, Font, FontSize, ShortLineRatio, LongLineRatio)*)
    }
    
    return, ret
  }
  
  /* 像下面这种行首带注释符号的情况，则删除注释符号及其后面的空白符
  ; aaaaaaaaaa            aaaaaaaaaa
  ; bbbbbbbbbb     =>     bbbbbbbbbb
  ; cccccccccc            cccccccccc
  */
  _deleteEscape(Text)
  {
    ; 注释符号可以在这里进行自定义
    oEscape:=[";", "#", "@REM"]
    
    ; 根据空行分段。只有整个段落的行首都有相同注释符，注释符才会被删除
    oParagraph1:=StrSplit(Text, "`n`n", " `t`r`n`v`f")
    
    ; 这里如果使用 for k, paragraph 则会在某些时候把 Class 自身给覆盖掉
    for k, paragraph1 in oParagraph1
    {
      oLines:=StrSplit(paragraph1, "`n")
      
      for k, Escape in oEscape
      {
        NeedleRegEx:=Format("^\s*\Q{}\E\s*", Escape)
        
        /* 像下面左边这种每行都有注释符才进行删除处理，右边这种不删除
        ; aaaaa            ; aaaaa
        ; bbbb             bbbb
        ; ccc              ; ccc
        */
        for k, line in oLines
        {
          if (RegExMatch(line, NeedleRegEx))
            needToDeleteEscape:=true
          else
          {
            needToDeleteEscape:=false
            break
          }
        }
        
        if (needToDeleteEscape)
        {
          for k, line in oLines
            ; 删掉注释符后，一并删掉句子左侧可能存在的不必要的空白符
            temp.=RegExReplace(line, NeedleRegEx, "", "", 1) "`n"
          
          break
        }
      }
      
      if (needToDeleteEscape)
      {
        ret.=temp "`n"
        temp:=""
      }
      else
        ret.=paragraph1 "`n`n"
    }
    
    return, RTrim(ret, "`n")
  }
  
  /* 像下面左边这种会被分成 1 2 987 988 共4段，右边会被分成 1-3 987 988 共3段
  1. aaaa              1. aaaa
  2. bbbb              3. bbbb
  987. dddd            987. dddd
  988. eeee            988. eeee
  */
  _splitBySerialNumber(oLines)
  {
    FSM:=[]
    for i, line in oLines
    {
      /* 像下面这种带序号的情况，匹配1 2，不匹配3.1
      1.aa文字
        2. aa文字
      3.1 aa文字
      */
      haveSerialNumber:=RegExMatch(line, "^\s*(\d+)\.[^0-9]", serialNumber)
      
      ; 以下 if 与 else if 的顺序是会影响结果的，没深刻理解就不要乱改
      if (i=1 and i=oLines.MaxIndex())
        FSM[i]:="start&end"
      else if (i=1)
        FSM[i]:="start"
      ; 序号递增1 或 第1次找到序号。这里存在隐藏条件 i>=2
      else if (serialNumber1-prevSerialNumber=1) or (haveSerialNumber and prevSerialNumber="")
      {
        ; 此 if 必须放在 FSM[i] 前面
        if (prevI and FSM[prevI]="normal")
        {
          FSM[prevI]:="start"
          FSM[prevI-1]:=(FSM[prevI-1]="start") ? "start&end" : "end"
        }
        
        FSM[i]:="start"
        FSM[i-1]:=(FSM[i-1]="start") ? "start&end" : "end"
      }
      else if (i=oLines.MaxIndex())
        FSM[i]:="end"
      else
        FSM[i]:="normal"
      
      if (haveSerialNumber)
      {
        prevSerialNumber:=serialNumber1
        prevI:=(i-1>=1) ? i : ""
      }
    }
    
    ; 根据序号标记生成文本
    for i, v in FSM
    {
      if (v="start&end" or v="end")
        Text.=oLines[i] "`n`n"
      else if (v="start" or v="normal")
        Text.=oLines[i] "`n"
    }
    Text:=RTrim(Text, "`n`n")
    
    return, StrSplit(Text, "`n`n")
  }
  
  _mergeByLen(Paragraph, Font, FontSize, ShortLineRatio, LongLineRatio)
  {
    ret:=[]
    
    oLines:=StrSplit(Paragraph, "`n")
    
    FSM:=[], sum:=0, n:=0, avg:=0
    for i, line in oLines
    {
      prevAvg:=avg
      len:=this._getLineLen(line, Font, FontSize)
      sum+=len, n++
      avg:=sum/n
      
      if (i=1 and i=oLines.MaxIndex())
        FSM[i]:="start&end"
      else if (i=1)
        FSM[i]:="start"
      else if (i=oLines.MaxIndex())
        FSM[i]:="end"
      else if (len/avg<=ShortLineRatio)
      {
        FSM[i]:="end"
        ; 不要在这里更新 avg 会导致 prevAvg 被错误更新
        sum:=0, n:=0
      }
      else if (len/prevAvg>=LongLineRatio)
      {
        FSM[i]:="start"
        FSM[i-1]:=(FSM[i-1]="start") ? "start&end" : "end"
        ; 一定要在这里更新 avg ，这样才会正确更新 prevAvg
        sum:=len, n:=1, avg:=sum/n
      }
      else
        FSM[i]:="normal"
    }
    
    for i, v in FSM
    {
      if (v="start&end" or v="end")
        Text.=oLines[i] "`n`n"
      else if (v="start" or v="normal")
        Text.=oLines[i] "`n"
    }
    Text:=RTrim(Text, "`n`n")
    
    for k, paragraph2 in StrSplit(Text, "`n`n")
    {
      str:=""
      
      oLines2:=StrSplit(paragraph2, "`n")
      
      ; 以整体文本判断语言
      lang:=this._getLang(paragraph2)
      
      ; 正式合并
      for k, v in oLines2
      {
        if (lang="en")
          /* 对行末的连接符进行删除处理
          Medical abor-      =>     Medical abortion in family
          tion in family
          */
          if (RegExMatch(oLines2[A_Index], "[a-zA-Z]+\-$"))
            str.=SubStr(oLines2[A_Index], 1, -1)
          /* 普通行则添加空格
          i love             =>     i love apple
          apple
          */
          else
            str.=oLines2[A_Index] A_Space
        
        if lang in zh,ja,ko,unknow
          str.=oLines2[A_Index]
      }
      
      ; 删除英文合并过程中在末尾产生的多余空格
      if (lang="en" and SubStr(str, 0, 1)=A_Space)
        str:=SubStr(str, 1, -1)
      
      /* 像下面这种行首带注释的情况，会在这里产生空行，所以强制有内容才能写入
      ; aaaaaaa
      ; aaaaaa
      ; 
      ; bbbbbbb
      */
      if (Trim(str, " `t`r`n`v`f")!="")
        ret.Push(str)
    }
    
    return, ret
  }

  _getLineLen(Text, Font, FontSize)
  {
    return, btt(Text,,,
    , {Font:Font, FontSize:FontSize}
    , {JustCalculateSize:true}).w
  }
  
  _getLang(Text)
  {
    RegExReplace(Text, "[a-zA-Z]",, English_Characters_Len)              ; 英文
    RegExReplace(Text, "[\x{4e00}-\x{9fa5}]",, Chinese_Characters_Len)   ; 中文
    RegExReplace(Text, "[\x{0800}-\x{4e00}]",, Japanese_Characters_Len)  ; 日文
    RegExReplace(Text, "[\x{ac00}-\x{d7ff}]",, Korean_Characters_Len)    ; 韩文
    
    if (English_Characters_Len/StrLen(Text) > 0.6)
      return, "en"
    else if (Chinese_Characters_Len/StrLen(Text) > 0.6)
      return, "zh"
    else if (Japanese_Characters_Len/StrLen(Text) > 0.5)
      return, "ja"
    else if (Korean_Characters_Len/StrLen(Text) > 0.5)
      return, "ko"
    else
      return, "unknow"
  }
}